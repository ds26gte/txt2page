" last modified 2015-05-26
" ds26gte@yahoo.com

func! s:recognizeUrls()

    " someone@gmail.com becomes mailto:someone@gmail.com
    " -- check not already preceded by mailto:
    s#\%(mailto:\)\@<!\<[[:alnum:]]\+@[[:alnum:].]\+\.\%(ca\|com\|edu\|gov\|in\|net\|org\|us\)\>#mailto:&#g

    " evalwhen.com becomes FAKEHTTP://evalwhen.com
    " -- check not preceded by @ or ://
    s#\%(@\|://\)\@<!\<[[:alnum:].-]\+\.\%(com\|co\.[[:lower:]]\{2}\|edu\|net\|org\|[[:lower:]]\{2}\%(js\|ms\|on\|ps\|sh\)\@<!\)\>#FAKEHTTP://&#g

    " mailto:addr becomes <a href="mailto:addr">mailto:addr</a>
    s#\<mailto:\([[:alnum:]]\+@[[:alnum:].]\+[[:alpha:]]\)\>#<a href="&"><span class=url>\1</span></a>#g

    " @twitterhandles
    s#\%([[:alnum:]]\)\@<!@\([[:alnum:]_]\{1,15}\)#*<span class=url>@\1</span>* https://twitter.com/\1#g

    " URL becomes <a href="URL">URL</a>
    s#\<[[:alpha:]-]\+://[^[:space:]()<>\[\]]\+\%(([[:alnum:]]\+)\|/\|[^[:space:][:punct:]¬ª]\)#<a\r√ûtzpHrefTzphref="&"\r><span class=url>&</span></a>#g

    " ‚Äú./sth‚Äù becomes <a href="sth">sth</a>
    s#‚Äú\./\(.\{-}\)‚Äù#<a href="\1"><span class=url>\1</span></a>#g

    " ./pathname becomes <a href="pathname">pathname</a>
    s#\%([.]\)\@<!\./\([^[:space:]()<>&]\+\)\%([[:punct:]]\)\@<!#<a href="\1"><span class=url>\1</span></a>#g

    " FAKEHTTP has done its job by now -- remove it
    "s#FAKEHTTP://##g

    " :emoji:
    s#:\([a-z0-9_\-]\{1,32}\):#<img class=emoji title=":\1:" alt=":\1:" src="https://assets.github.com/images/icons/emoji/\1.png" height=20 width=20 align=absmiddle/>#g

    "images

    s#^\.\s*IMG\s\+\([^-[:space:]]\)#.IMG -center \1#

    s#^\.\s*IMG\s\+-L\s#.IMG -left #
    s#^\.\s*IMG\s\+-C\s#.IMG -center #
    s#^\.\s*IMG\s\+-R\s#.IMG -right #

    s#^\.\s*\(IMG\s\+-[leftcnrigh]\+\s\+\S\+\)\s*$#.\1 50#

    s#^\.\s*IMG\s\+-\([leftcnrigh]\+\)\s\+\(\S\+\)\s\+\([0-9]\+\)#<div class=figure align=\1><img src="\2" width="\3%" border="0" alt=""></div>#

    exec 's:<a href="\(.\{-}\)\.' . g:fileExtension . '"><span class=url>\(.\{-}\)\.' . g:fileExtension . '</span></a>:<a href="\1.html"><span class=url>\2.html</span></a>:g'

    exec 's:<a href="\(.\{-}\)\.' . g:fileExtension . '">:<a href="\1.html">:g'

    exec 's:<a href="\(.\{-}\)\.' . g:fileExtension . '#\(.\{-}\)"><span class=url>\%(.\{-}\)\.' . g:fileExtension . '#\(.\{-}\)</span></a>:<a href="\1.html#\2">\3</a>:g'

    s:\(<a href="#.\{-}">\)<span class=url>#\(.\{-}\)</span>:\1\2:g

endfunc

func! s:cleanUpUrls()
    s/√ûtzpHrefTzphref/ href/
    s#href="FAKEHTTP://#href="http://#
    -1,+1 jo!
    s#FAKEHTTP://##g
    s:¬´\(<a href.\{-}/a>\)¬ª:\1:g
endfunc

func! s:upcaseDigits(x)
  let l:y = a:x
  let l:y = substitute(l:y, '0', '‚Å∞', 'g')
  let l:y = substitute(l:y, '1', '¬π', 'g')
  let l:y = substitute(l:y, '2', '¬≤', 'g')
  let l:y = substitute(l:y, '3', '¬≥', 'g')
  let l:y = substitute(l:y, '4', '‚Å¥', 'g')
  let l:y = substitute(l:y, '5', '‚Åµ', 'g')
  let l:y = substitute(l:y, '6', '‚Å∂', 'g')
  let l:y = substitute(l:y, '7', '‚Å∑', 'g')
  let l:y = substitute(l:y, '8', '‚Å∏', 'g')
  let l:y = substitute(l:y, '9', '‚Åπ', 'g')
  return l:y
endfunc

func! s:findQvUrls()
  v/^√ûtzpPreformattedTzp/ s_\(\\\*\[:\)\s*\%(\\\)\?\s*$_\1√ûtzpQvUrlContinuedTzp_
  g/√ûtzpQvUrlContinuedTzp$/ .,+1 j!
  %s/√ûtzpQvUrlContinuedTzp/ /
  v/^√ûtzpPreformattedTzp/ s/\(\S\)\(\\\*\[:\)/\1 \2/

  v/^√ûtzpPreformattedTzp/ s_\\\*\[:\s\+\(.\{-}\)\s*\]_:\1_g
  v/^√ûtzpPreformattedTzp/ s_:\s*<a href="\(.\{-}\)">.\{-}</a>_<a href="\1">√ûtzpQvUrlTzp</a>_g
  %s/√ûtzpQvUrlTzp/(ùëû.ùë£.)/g
  "%s/√ûtzpQvUrlTzp/(q.v.)/g
  " ùíí.ùíó. ùìÜ.ùìã. ùì∫.ùìø. ùîÆ.ùî≥. ùï¢.ùïß. ùññ.ùñõ. ùóæ.ùòÉ.
endfunc

func! s:findUrlhs()
  g/\%(\%(-:\|[·õ´‚Ä°]\).*\)\@<!<a href="/ s/^/√ûtzpPossibleUrlhTzp/

  g/^√ûtzpPossibleUrlhTzp/ -1s/\%(-:\|[·õ´‚Ä°]\).\{-}$/&√ûtzpUrlhContinuationLineTzp/

  g/^√ûtzpPossibleUrlhTzp/ -2s/\%(-:\|[·õ´‚Ä°]\).\{-}$/&√ûtzpUrlhContinuationLineTzp/

  g/√ûtzpUrlhContinuationLineTzp$/ -1s/√ûtzpUrlhContinuationLineTzp$//

  g/√ûtzpUrlhContinuationLineTzp$/ .,/^√ûtzpPossibleUrlhTzp/ j

  %s/√ûtzpPossibleUrlhTzp//

  "g/√ûtzpUrlhContinuationLineTzp$/ .,+1 j

  %s/√ûtzpUrlhContinuationLineTzp//

  v/^√ûtzpPreformattedTzp/ s#\%(-:\|[·õ´‚Ä°]\)\s*\(.\{-}\)\s*<a href="\(.\{-}\)">.\{-}</a>#<a href="\2">\1</a>#g

endfunc

func! s:redirectIfNecessary()
  v/^√ûtzpPreformattedTzp/ s#<a href="\(.\{-}\)">=REDIRECT=</a>#√ûtzpRedirectTzp{\1}#
  let redirectFoundP = 0
  g/√ûtzpRedirectTzp{.\{-}}/ let redirectFoundP = 1
  if redirectFoundP
    /√ûtzpRedirectTzp/
    norm f{lyi}
    let g:redirectUrl = @0
    g/√ûtzpRedirectTzp{.\{-}}/ s#√ûtzpRedirectTzp{\(.\{-}\)}#If not redirected automatically, go to <a href="\1">\1</a>.#
  endif
endfunc

func! s:verbatimLineAsIsWithContinuation()
  s/\s/√ò/g
  s/^[^√ò]/√ûtzpVerbatimContinuationTzp&/
  s/^√ò//
  s/\([^√ò]\)√ò\([^√ò]\)/\1 \2/g
  s#$#√ûtzpBreakLineTzp#
endfunc

func! s:verbatimLineAsIs()
  s/^$/√ò/
  s/\s/√ò/g
  s/\([^√ò]\)√ò\([^√ò]\)/\1 \2/g
  s#$#<br/>#
endfunc

func! s:codeLineAsIs()
  call s:verbatimLineAsIs()
  s/^/√ûtzpPreformattedTzp/
endfunc

func! Txt2page()

  setl fo-=j

while 1
  if (!$soelim)
    %s/^\.\s*so\s\+\(\S\+\s\+\S\)/√ûtzpSoTOCtzp \1/
  endif
  0
  let m = search('^\.\s*so\s')
  if (m == 0)
    break
  endif
  s/^\.\s*so/√ûtzpSoDoneTzp/
  norm W
  r <cfile>
endwhile

g/^√ûtzpSoDoneTzp/d

"convert all setminuses (U+2216) to backslashes
"(setminus is used in troff source as a convenient nonactive stand-in for backslash)

%s/‚àñ/\\\\/g

"use unabbreviations for these rare characters if they occur verbatim in the source,
"as we'll be using these characters as temporary symbols

%s/√û/√ûtzpThornTzp/g
%s/^√ûtzpThornTzp\(tzpSoTOCtzp\)/√û\1/  "! see above
%s/√ò/√ûtzpOslashTzp/g
%s/√Ü/√ûtzpAEligTzp/g
%s/¬´/√ûtzpLangleLangleTzp/g
%s/¬ª/√ûtzpRangleRangleTzp/g

$a
√ûtzpBogusEndOfFileLineTzp
.

"use temp symbols to stand for verbatim &<>; we'll convert them to appropriate HTML entities
"at the end

%s/&/√Ü/g
%s/</¬´/g
%s/>/¬ª/g

"remove all trailing spaces

%s/\s\+$//

let titleText = $g

let g:fileExtension = $ext
if g:fileExtension == ""
    let g:fileExtension = "tzpLetsNotMatchAnythingAgainstThis"
endif

0
let lastline = line('$')
while 1
    let linenum = line('.')
    if linenum > 100 || linenum == lastline
        break
    endif
    let linestr = getline('.')
    if match(linestr, '^\.\s*TH\s\+".\{-}"') > -1
        s/^\.\s*TH\s\+"\(.\{-}\)".*/.TH \1/
        break
    elseif match(linestr, '^\.\s*TH\s\+\(\S\+\)\s\+\S') > -1
        s/^\.\s*TH\s\+\(\S\+\).*/.TH \1/
        break
    endif
    norm j
endwhile

"calc titleText here?

"code display

" troff-friendly triple-backquote env

%s/^\s*\(\`\`\`\+\)/.\1/

%s/^\.\s*\`\`\`\`\+/.EE/

%s/^\.\s*\`\`\`/.EX/

%s/^\.\s*EX\s*[[:alpha:]]\+$/.EX scheme/

if executable('lisphilite')
  g/^\.\s*EX scheme/+1,/^\.\s*EE/-1 s/¬´/</g
  g/^\.\s*EX scheme/+1,/^\.\s*EE/-1 s/¬ª/>/g
  g/^\.\s*EX scheme/+1,/^\.\s*EE/-1 s/√Ü/\&/g
  g/^\.\s*EX scheme/+1,/^\.\s*EE/-1 !lisphilite
endif

g/^\.\s*EX/+1, /^\.\s*EE/-1 call s:codeLineAsIs()

%s#^\.\s*EX\(.*\)#</p>\r√ûtzpPreformattedTzp<div class="listing\1"><code>#

%s#^\.\s*EE.*#√ûtzpPreformattedTzp</code></div>\r<p class=noindent>#

"

g/^\.\s*TOC$/d

"

      $a
√ûtzpFiTzp
.

%s/^\.\s*\(nf\|fi\)\(\s\+.*\)\?/.\1/

g/^\.\s*nf$/+1,/^\%(\.\s*fi\|√ûtzpFiTzp\)$/-1 call s:verbatimLineAsIs()

g/^\.\s*\%(nf\|fi\)$/d

g/^√ûtzpFiTzp/d

0
let lastline = line('$')
while 1
  " find the first line with a leading space
  let currline = line('.')
  if currline == 1 && match(getline('.'), '^\s') > -1
    let m = 1
  else
    let m = search('^\s')
  endif
  if m == 0
    break
  endif
  " and mark its end
  s/$/√ûtzpLeadingSpacesStartLineTzp/
  while 1
    " if blank line found, make it a parsep
    if match(getline('.'), '^$') > -1
      s:^$:</p>√ûtzpCarriageReturnTzp<p class=noindent>:
      . -1s:√ûtzpBreakLineTzp$::
      break
    endif
    " if not a blank line, verbatimize it
    call s:verbatimLineAsIsWithContinuation()
    " break on eof
    if line('.') == lastline
      break
    endif
    norm j
  endwhile
  " put a parsep before the start too
  g/√ûtzpLeadingSpacesStartLineTzp/ s#^#</p>√ûtzpCarriageReturnTzp<p class=noindent>√ûtzpCarriageReturnTzp#

  %s/√ûtzpLeadingSpacesStartLineTzp//

  g/^√ûtzpVerbatimContinuationTzp/-1 s:√ûtzpBreakLineTzp$::

  %s/^√ûtzpVerbatimContinuationTzp//
endwhile
" make the parseps neat
%s:√ûtzpCarriageReturnTzp:\r:g

%s:√ûtzpBreakLineTzp$:<br/>:

"v/\%(-:\|[·õ´‚Ä°]\)/ s/^\.\s*\\\"/√ûtzpDeleteCommentTzp&/
"g/^√ûtzpDeleteCommentTzp/d

"%s/^\.\s*\\\"//

g/^\.\s*\\\"/d
"g/^\.\s*HTL/d
"g/^#\s\+.\{-}\s*##$/d

"bullet items

%s:^‚Ä¢\s\+:</p><p class=bulleted><span class=bullet>‚Ä¢√ò</span>:

"Tables

"for every line starting with |\s, replace it with <space>√ûtzpTableLineTzp

g/^|\s/,/^[^|]/-1 s#^|\s# √ûtzpTableLineTzp#

"collapse successive √ûtzpTableLineTzps together

g/^ √ûtzpTableLineTzp/,/^\%($\|[^ ]\)/-1 j

"remove single space before √ûtzpTableLineTzp

%s/ \(√ûtzpTableLineTzp\)/\1/g

"enclose the collapsed √ûtzpTableLineTzp with <table> tags

%s#^√ûtzpTableLineTzp.*$#</p>\r<div align=center>\r<table border="1" cellpadding="4">&\r</table>\r</div>\r<p>#

"precede each √ûtzpTableLineTzp with a CR

%s#√ûtzpTableLineTzp#\r&#g

"chomp the last | in each table line

g/^√ûtzpTableLineTzp/ s#\s|$##

"replace every other | with a table-cell separator

g/^√ûtzpTableLineTzp/ s#\s|\s#</td><td>#g

"wrap each table-line with a <tr>

%s#^√ûtzpTableLineTzp\(.*\)$#<tr><td>\1</td></tr>#

"g/^√ûtzpBogusEndOfFileLineTzp/d

"visible anchor
"%s:^‚Äª\s*\([^[:space:]()<>&#]\+\)$:<span class=anchor><a name="\1"></a>[\1]√ò</span>:
%s:^\.\s*BB\s\+\([^[:space:]()<>&#]\+\)$:<span class=anchor><a name="\1"></a>[\1]</span>:

"invisible anchor
"%s:^‚öì\s*\([^[:space:]()<>&#]\+\)$:<a name="\1"></a>:
%s:^\.\s*AN\s\+\([^[:space:]()<>&#]\+\)$:<a name="\1"></a>:

"sections

" troff-friendly .#...

%s/^\.\s*\(#\+\)/\1/

%s/^\.\s*TH\s\+\(.*\)$/√ûtzpSectionTzp title \1/
%s/^\.\s*SH\s\+\(.*\)$/√ûtzpSectionTzp 1 \1/
%s/^\.\s*SS\s\+\(.*\)$/√ûtzpSectionTzp 2 \1/

%s/^#\s\+\(.\{-}\)\s\+#$/√ûtzpSectionTzp title \1/
%s/^#\s\+\(.\{-}\)\s\+##$/√ûtzpSectionTzp htmltitle \1/
%s/^###\s\+###$/√ûtzpSectionTzp dropcap x/

%s/^#\s\+\(.\{-}\)\s*#*$/√ûtzpSectionTzp 1 \1/
%s/^##\s\+\(.\{-}\)\s*#*$/√ûtzpSectionTzp 2 \1/
%s/^###\s\+\(.\{-}\)\s*#*$/√ûtzpSectionTzp 3 \1/
%s/^####\s\+\(.\{-}\)\s*#*$/√ûtzpSectionTzp 4 \1/
%s/^#####\s\+\(.\{-}\)\s*#*$/√ûtzpSectionTzp 5 \1/
%s/^######\s\+\(.\{-}\)\s*#*$/√ûtzpSectionTzp 6 \1/

%s/^\.\s*=\{1}\s\+\(\S\)/√ûtzpTroffSectionTzp 1 \1/
%s/^\.\s*=\{2}\s\+\(\S\)/√ûtzpTroffSectionTzp 2 \1/
%s/^\.\s*=\{3}\s\+\(\S\)/√ûtzpTroffSectionTzp 3 \1/
%s/^\.\s*=\{4}\s\+\(\S\)/√ûtzpTroffSectionTzp 4 \1/
%s/^\.\s*=\{5}\s\+\(\S\)/√ûtzpTroffSectionTzp 5 \1/
%s/^\.\s*=\{6}\s\+\(\S\)/√ûtzpTroffSectionTzp 6 \1/

g/^√ûtzpTroffSectionTzp/,/^$/-1 j

g/^√ûtzpSectionTzp/,/./-1 j!

%s/^√ûtzpTroffSectionTzp/√ûtzpSectionTzp/

g/^√ûtzpSectionTzp\s\+htmltitle\s\+/d

%s:^√ûtzpSectionTzp\s\+\(title\)\s\+\(.*\):</p>\r<h1 class=\1>\2</h1>\r<p class=noindent>:
%s:^√ûtzpSectionTzp\s\+\([1-6]\)\s\+\(.*\):</p>\r<h\1 class=section>\2</h\1>\r<p class=noindent>:

let lastline = line('$')
let ln = 0
while ln < 100 && ln < lastline
  let x = getline(ln)
  if match(x, '^<h[0-9]\+.\{-}>') > -1
    let titleText = substitute(x, '^<h[0-9]\+.\{-}>\(.\{-}\)<\/h[0-9]>', '\1', '')
    break
  endif
  let ln+=1
endwhile

"drop cap

%s/^√ûtzpSectionTzp\s\+dropcap\s\+.*/√ûtzpDropCapTzp/

g/^√ûtzpDropCapTzp$/ , +1 j

%s:^√ûtzpDropCapTzp\s*\(.\{-}[[:alnum:]]\):</p>\r<p class=noindent><span class=dropcap>\1</span>:

"

v/^√ûtzpPreformattedTzp/ s/^\([^@].\{-}\\\*\[\%(\^\|::\)\s*[^]]\{-}\)\\\$/\1 √ûtzpContinuationLineTzp/

g/\\\*\[\%(\^\|::\)\s*[^]]\{-}√ûtzpContinuationLineTzp$/ .,+1 j!

%s/√ûtzpContinuationLineTzp//

"footnotes

" (‚Ä†sym) becomes call to footnote

v/^√ûtzpPreformattedTzp/ s:(‚Ä†\([^()[:space:]]\+\)):<a name="callFootnote_\1" href="#footnote_\1">√ûtzpFootnoteMarkTzp\1√ûtzpFootnoteMarkEndTzp</a>:g

" ensure footnote text start/end is at bol

v/^√ûtzpPreformattedTzp/ s:\(.\)\((‚Ä†\|‚Ä†)\):\1\r\2:g

" put footnote text's symbol line on own line

%s:^(‚Ä†\s*\(\S\+\)\s\+\(\S\):(‚Ä†\1\r\2:

" move text following footnote text's end to a new line after

%s:^\(‚Ä†)\)\s*\(\S\):\1\r\2:

" mark footnote text start line

%s:^(‚Ä†\s*\(\S\+\)\?\s*$:√ûtzpFootnoteEnvTzp \10:

%s:^\.FS\s*\(\S\+\)\?\s*$:√ûtzpFootnoteEnvTzp \10:

" mark its end line

%s:^‚Ä†)\s*$:√ûtzpFootnoteEnvTzp1:

%s:^\.FE\s*$:√ûtzpFootnoteEnvTzp1:

" if footnote text has no symbol, specify a dummy symbol

%s/^\(√ûtzpFootnoteEnvTzp\)0$/\1 √ûtzpUnmarkedFootnoteTzp0/

"put a √ûtzpFootnoteTzp before every footnote line
g/^√ûtzpFootnoteEnvTzp.*0$/,/^√ûtzpFootnoteEnvTzp.*1$/ s/^/√ûtzpFootnoteTzp/

"wrap all footnote lines from one footnote into one combined line
g/^√ûtzpFootnoteTzp√ûtzpFootnoteEnvTzp.*0$/,/^√ûtzpFootnoteTzp√ûtzpFootnoteEnvTzp.*1$/ j!

"move all the footnote combilines to the file's end
g/^√ûtzpFootnoteTzp√ûtzpFootnoteEnvTzp/ m$

"put a √ûtzpStartFootnotesTzp ahead of the first footnote combiline
/^√ûtzpFootnoteTzp√ûtzpFootnoteEnvTzp/ s/^/√ûtzpStartFootnotesTzp\r/

"put a √ûtzpEndFootnotesTzp after the last footnote combiline
$ s/^\(√ûtzpFootnoteTzp√ûtzpFootnoteEnv.*\)/\1\r√ûtzpEndFootnotesTzp/

"use √ûtzp{Start,End}FootnotesTzp to wrap a <div>

/^√ûtzpStartFootnotesTzp/ s#^.*$#</p>\r<div class=footnotes>\r<hr align=left width="40%"/><p>#
/^√ûtzpEndFootnotesTzp/   s#^.*$#</p>\r</div><p>#

"use √ûtzpFootnoteTzp to un-combine the combilines into individual lines again
%s/\(.\)\(√ûtzpFootnoteTzp\)/\1\r\2/g

"do it once more to pick up adjacent √ûtzpFootnoteTzp's
%s/\(.\)\(√ûtzpFootnoteTzp\)/\1\r\2/g

"throw away each footnote's end marker: its job is done
%s/^√ûtzpFootnoteTzp√ûtzpFootnoteEnv.*1$//

"use each footnote's start marker to typeset its symbol (if any)

%s:^√ûtzpFootnoteTzp√ûtzpFootnoteEnvTzp √ûtzpUnmarkedFootnoteTzp0$:\r:

%s:^√ûtzpFootnoteTzp√ûtzpFootnoteEnvTzp \(\S\+\)0$:\r<a name="footnote_\1" href="#callFootnote_\1">√ûtzpFootnoteMarkTzp\1√ûtzpFootnoteMarkEndTzp</a>:

%s:^√ûtzpFootnoteTzp::

%s/√ûtzpFootnoteMarkTzp\(.\{-}\)√ûtzpFootnoteMarkEndTzp/\=s:upcaseDigits(submatch(1))/g

"end footnotes

"%s#^:\([^:]\+\):#</p>\r<p class=noindent><span class=dropcap>\1</span>#

"%s#^:\s\+\(\%([0-9]\+\.\)*[0-9]\+\)\.\s#</p>\r<p class=beginsection><span class=dropcap>\1</span> #
"%s#^:\s\+\([^A-Z0-9]*[A-Z0-9]\)#</p>\r<p class=beginsection><span class=dropcap>\1</span>#

%s#^\s*-\{5,}$#<hr/>#
%s#^\.\s*--\(\s.*\)\?$#<hr noshade size="1"/>#
%s#^\.\s*==\(\s.*\)\?$#<hr noshade size="5"/>#

"

"if "$prevPage" != "" || "$nextPage" != ""
"  1s:^:√ûtzpNavbarTzp<a href="$prevPage">previous</a>, <a href="$nextPage">next</a> page\r:
"  $s:$:\r√ûtzpNavbarTzp<a href="$prevPage">previous</a>, <a href="$nextPage">next</a> page:
"endif
"
"g/^√ûtzpNavbarTzp/ s:<a href="">\(.\{-}\)</a>:<span class=grayed>\1</span>:g
"
"%s:^√ûtzpNavbarTzp\(.*\)$:<div align=right><span class=navbar>[Go to \1]</span></div>:

/^\.NAV/ m $

      $ /^\.NAV/ co 0

%s:^\.NAV.*:\r\0\r:

%s:^\.NAV\s\+\(\S\+\)\s\+\(\S\+\)\s\+\(\S\+\)\s\+\(\S\+\):√ûtzpNavbarTzp<a href="\1">previous</a>, <a href="\2">next</a> page; <a href="\3">contents</a>; <a href="\4">index</a>:
%s:^\.NAV\s\+\(\S\+\)\s\+\(\S\+\)\s\+\(\S\+\):√ûtzpNavbarTzp<a href="\1">previous</a>, <a href="\2">next</a> page; <a href="\3">contents</a>:
%s:^\.NAV\s\+\(\S\+\)\s\+\(\S\+\):√ûtzpNavbarTzp<a href="\1">previous</a>, <a href="\2">next</a> page:
%s:^\.NAV\s\+\(\S\+\):√ûtzpNavbarTzp<a href="\1">next</a> page:

g/^√ûtzpNavbarTzp/ s:<a href="_">\(.\{-}\)</a>:<span class=grayed>\1</span>:g

%s:^√ûtzpNavbarTzp\(.*\)$:<div align=right><span class=navbar>[Go to \1]</span></div>:

%s:^√ûtzpSoTOCtzp\s\+\(\S\+\)\s\+\(.*\):<a href="\1">\2</a><br/>:

"empty lines are paragraph separators, but collapse adjacent empty lines first

v/./,/./-j

%s#^$#</p>\r\r<p>#

"g/^<p>$/,+1 j!

"%s#^<p>\(\%([A-Z0-9]\+\.\)\+\)\s#<p><b>\1</b> #

v/^√ûtzpPreformattedTzp/ s#\`\`\(.\{-1,}\)\`\`#<code>\1</code>#g

v/^√ûtzpPreformattedTzp/ s#\`\(.\{-1,}\)\`#<code>\1</code>#g

g/^√ûtzpBogusEndOfFileLineTzp/d

v/^√ûtzpPreformattedTzp/ call s:recognizeUrls()

g/^√ûtzpHrefTzphref=/ call s:cleanUpUrls()

call s:findQvUrls()

call s:findUrlhs()

call s:redirectIfNecessary()

%s/^√ûtzpPreformattedTzp//
%s/√ûtzpPreformattedTzp/\r/g

%s/¬´/\&lt;/g
%s/¬ª/\&gt;/g
%s/√Ü/\&amp;/g
%s/√ò/¬†/g "space is actually u+00a0

%s/√ûtzpThornTzp/√û/g

%s/√ûtzpAEligTzp/√Ü/g
%s/√ûtzpLangleLangleTzp/¬´/g
%s/√ûtzpOslashTzp/√ò/g
%s/√ûtzpRangleRangleTzp/¬ª/g

"add html boilerplate and title

0i
<!DOCTYPE html>
<html lang="en">
<!--
Generated from MYSOURCEFILE by txt2page, version MYVERSION
Copyright (C) 2010-2015 Dorai Sitaram
-->
<head>
<meta charset="utf-8">
<link rel="stylesheet" href="MYCSSFILE" />
<title>MYTITLE</title>
</head>
<body>
<p>
.

$a
</p>
</body>
</html>
.

exec '5,10s#MYTITLE#' . $g . '#'

exec '1,5s#MYSOURCEFILE#' . $f . '#'

exec '1,5s#MYVERSION#' . $ver . '#'

exec '5,10s#MYCSSFILE#' . $cssf . '#'

if exists('g:redirectUrl')
  5,15s#</head>#<meta http-equiv="refresh" content="1;√ûtzpRedirectUrlTzp">\r\0#
  let @0 = g:redirectUrl
  5,15s/√ûtzpRedirectUrlTzp/\=@0/
endif

endfunc
